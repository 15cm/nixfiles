setopt autocd
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus

alias -g ...='../..'
alias -g ....='../../..'
alias -g .....='../../../..'
alias -g ......='../../../../..'
alias -- -='cd -'
alias 1='cd -1'
alias 2='cd -2'
alias 3='cd -3'
alias 4='cd -4'
alias 5='cd -5'
alias 6='cd -6'
alias 7='cd -7'
alias 8='cd -8'
alias 9='cd -9'
# Allows alias after sudo. If the last character of the alias value is a space
# or tab character, then the next command word following the alias is also
# checked for alias expansion.
# http://www.gnu.org/software/bash/manual/bashref.html#Aliases
alias sudo="sudo "
alias _="sudo"

# Disable Ctrl-s which freezes the terminal
stty -ixon

# Enable editing current command in editors.
autoload edit-command-line
zle -N edit-command-line

# <tab> selection color
zstyle ':completion:*' menu select

# Updates editor information when the keymap changes.
function zle-keymap-select() {
  zle reset-prompt
  zle -R
}
zle -N zle-keymap-select

# Ensure that the prompt is redrawn when the terminal size changes.
TRAPWINCH() {
  zle &&  zle -R
}

# zle keybindings
autoload -Uz edit-command-line
bindkey -a '^[v' edit-command-line # equivalent to 'bindkey -M vicmd'
bindkey '^[v' edit-command-line

# Mainly to accept a word in zsh auto suggestion.
bindkey '^o' forward-word

# The key code is obtained by pressing the key in the input of `cat`.
# https://stackoverflow.com/questions/8638012/fix-key-settings-home-end-insert-delete-in-zshrc-when-running-zsh-in-terminat
bindkey -M vicmd "^[[1~" beginning-of-line
bindkey -M viins "^[[1~" beginning-of-line
bindkey -M vicmd "^[[4~" end-of-line
bindkey -M viins "^[[4~" end-of-line

# zce
bindkey -M vicmd 't' zce

bindkey -s '^u' 'ya^m'

# direnv
if which direnv > /dev/null; then eval "$(direnv hook zsh)"; fi

# --------------------- exa ---------------------
if which exa > /dev/null; then
  _ls_cmd="eza --color always --group-directories-first --sort extension"
  alias ls="$_ls_cmd"
  alias la="$_ls_cmd -al --git --group --header"
  alias ll="$_ls_cmd -l --git --group --header"
  alias l="$_ls_cmd -l --git"
  alias tree="$_ls_cmd --tree --level 2 --git"
  _tree_cmd="$_ls_cmd --tree --level 2"
else
  _ls_cmd="ls --color=tty --group-directories-first"
  alias ls="$_ls_cmd"
  alias ll="$_ls_cmd -lh"
  alias l="$_ls_cmd -lh"
  alias la="$_ls_cmd -lAh"
  _tree_cmd="tree -C"
fi
# _____________________ exa _____________________

# Navi
bindkey '^y' _navi_widget

# --------------------- fzf ---------------------
export FZF_COMPLETION_TRIGGER=':'

_fzf_compgen_path() {
  fd --hidden --follow --exclude ".git" . "$1"
}

_fzf_compgen_dir() {
  fd -H --no-ignore-vcs -t d . "$1"
}

# fzf z cd
bindkey -s '^j' '__zoxide_zi^m'

# fzf dirs under current dir
__fzf_dir() {
  set -o nonomatch
  _fzf_compgen_dir . \
    | fzf --tac --reverse --preview "$_tree_cmd {} | head -200" \
    | while read item; do printf ' %q' "$item"; done
  echo
}

__fzf_dir_widget() {
  LBUFFER="${LBUFFER}$(__fzf_dir)"
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

zle -N __fzf_dir_widget

bindkey '^f' fzf-cd-widget
bindkey '^x' __fzf_dir_widget

# Remove alt bindings of fzf because it conflicts with zsh vi mode. Esc and Alt sends the same key code '^['.
bindkey -r viins '^[c'
bindkey -r vicmd '^[c'

# zprof
